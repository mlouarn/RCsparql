PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX user: <http://www.semanticweb.org/user/ontologies/2018/1#>
PREFIX : <http://www.semanticweb.org/user/ontologies/2018/1#>

INSERT {
  GRAPH user:TISSUE {
    _:idRel user:fromTF ?tf .
    _:idRel user:fromGene ?gene .
    _:idRel user:weightP ?Wp .
    _:idRel user:weightE ?We .
    _:idRel user:score ?score .
  }
}
WHERE {
  VALUES ?g1 {user:SAMPLE1}
  ?tf rdf:type user:tf .
  ?gene rdf:type user:gene .
  OPTIONAL {
    GRAPH ?g1 {
      ?rP user:fromTF ?tf.
      ?rP user:fromGene ?gene .
      ?rP user:weightP ?weightP1 .
    }
  }
BIND (IF(bound(?weightP1), ?weightP1, "0.0"^^xsd:float) AS ?Wp) .
  OPTIONAL {
    GRAPH ?g1 {
      ?rE user:fromTF ?tf.
      ?rE user:fromGene ?gene .
      ?rE user:weightE ?weightE1 .
    }
  }
BIND (IF(bound(?weightE1), ?weightE1, "0.0"^^xsd:float) AS ?We) .

BIND (IF((?We > ?Wp),?We, ?Wp) AS ?score)
FILTER (?score >0)
}
