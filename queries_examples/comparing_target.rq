#Comparing targets and their score of a given TF across 2 Tissues

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX user: <http://regulatorycircuits.org/data/>
PREFIX graph: <http://regulatorycircuits.org/graph/>
PREFIX rco: <http://regulatorycircuits.org/data/>

SELECT DISTINCT ?tf ?gene ?score_Tissue1 ?score_Tissue2
 WHERE {
  VALUES ?g1 {graph:anaplastic_carcinoma_cell_line} #anaplastic_carcinoma_cell_line can be changed to any other Tissue, see dictionary for names
  VALUES ?g2 {graph:anaplastic_large_cell_lymphoma_cell_line} #can be changed to another Tissue
  VALUES ?tf {user:POU6F1} #can be replaced with another TF
  ?gene rdf:type user:gene .
  OPTIONAL {
    GRAPH ?g1 {
      ?rP rco:fromTF ?tf.
      ?rP rco:fromGene ?gene .
      ?rP rco:scoreRC ?score_1 . #change scoreRC to score for the recomputed score using max
    }
}
BIND (IF(bound(?score_1), ?score_1, "0.0"^^xsd:float) AS ?score_Tissue1) .
  OPTIONAL {
    GRAPH ?g2 {
      ?rP rco:fromTF ?tf.
      ?rP rco:fromGene ?gene .
      ?rP rco:scoreRC ?score_2 . #change scoreRC to score for the recomputed score using max
    }
}
BIND (IF(bound(?score_2), ?score_2, "0.0"^^xsd:float) AS ?score_Tissue2) .
BIND (xsd:float(xsd:float(?score_Tissue1) + xsd:float(?score_Tissue2)) AS ?result)
FILTER (?result >0)
}
 #
